buildscript {
    ext.kotlin_version = '1.3.72'

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id "com.vanniktech.maven.publish" version "0.11.1"
    id "org.jlleitschuh.gradle.ktlint" version "9.2.1"
}

allprojects {
    repositories {
        mavenCentral()
        google()
        jcenter()
    }
}

ktlint {
    version = "0.34.2"
}

dependencies {
    // This includes the kotlin runtime in to the fat jar
    implementation 'com.github.javaparser:javaparser-symbol-solver-core:3.6.25'
    implementation 'com.github.ajalt:clikt:2.6.0'
//    implementation 'net.java.dev.jna:jna:5.5.0'
    compile project(':kastree:ast-psi')
    testImplementation  "junit:junit:4.13"
    testImplementation "com.google.truth:truth:1.0.1"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

jar {
    dependsOn(shadowJar)

    manifest {
        attributes 'Main-Class': 'com.dropbox.mobile.hypershard.HypershardKt'
    }
}

shadowJar {
    classifier = "all"
}

artifacts {
    runtime shadowJar
    archives shadowJar
}

project(':kastree:ast:ast-common') {
    apply plugin: 'kotlin-platform-common'
    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlin_version"
        testCompile "org.jetbrains.kotlin:kotlin-test-annotations-common:$kotlin_version"
        testCompile "org.jetbrains.kotlin:kotlin-test-common:$kotlin_version"
    }
}

project(':kastree:ast:ast-jvm') {
    apply plugin: 'kotlin-platform-jvm'
    sourceCompatibility = '1.8'
    compileKotlin {
        kotlinOptions.jvmTarget = '1.8'
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = '1.8'
    }
    dependencies {
        expectedBy project(':kastree:ast:ast-common')
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        testCompile 'junit:junit:4.12'
        testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
        testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    }
    test {
        testLogging {
            outputs.upToDateWhen {false}
            showStandardStreams = true
            exceptionFormat = 'full'
            events 'passed', 'skipped', 'failed'
        }
    }
}

project(':kastree:ast-psi') {
    apply plugin: 'kotlin'
    sourceCompatibility = '1.8'
    compileKotlin {
        kotlinOptions.jvmTarget = '1.8'
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = '1.8'
    }
    dependencies {
        compile project(':kastree:ast:ast-jvm')
        compile 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
        compile "org.jetbrains.kotlin:kotlin-compiler-embeddable:$kotlin_version"
        compile 'net.java.dev.jna:jna:5.5.0'
        testCompile 'junit:junit:4.12'
        testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
        testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    }
    test {
        testLogging {
            outputs.upToDateWhen {false}
            showStandardStreams = true
            exceptionFormat = 'full'
            events 'failed'
        }
        environment 'KOTLIN_REPO', '/Users/gogo/kotlin'
    }
}